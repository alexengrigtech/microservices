plugins {
    id 'org.springframework.boot' apply false
    id 'io.spring.dependency-management' apply false
    id 'io.github.alexengrig.spring-banner' apply false
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'io.github.alexengrig.spring-banner'

    group = 'dev.alexengrig.microservice'
    version = '0.1.0-SNAPSHOT'

    sourceCompatibility = '17'

    springBoot {
        buildInfo()
    }

    repositories {
        mavenCentral()
    }

    dependencyManagement {
        imports {
            mavenBom("org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}")
        }
    }

    dependencies {
        implementation('org.springframework.boot:spring-boot-starter')
        testImplementation('org.springframework.boot:spring-boot-starter-test')

        implementation('org.springframework.boot:spring-boot-starter-actuator')

        // Spring Cloud LoadBalancer is currently working with the default cache.
        // While this cache implementation is useful for development and tests,
        // it's recommended to use Caffeine cache in production.
        // You can switch to using Caffeine cache,
        // by adding it and org.springframework.cache.caffeine.CaffeineCacheManager to the classpath.
        implementation('com.github.ben-manes.caffeine:caffeine')

        compileOnly('org.projectlombok:lombok')
        annotationProcessor('org.projectlombok:lombok')
        testCompileOnly('org.projectlombok:lombok')
        testAnnotationProcessor('org.projectlombok:lombok')
    }

    test {
        useJUnitPlatform()
    }
}
